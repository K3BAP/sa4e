plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.3'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

repositories {
    mavenCentral()
}

javafx {
    version = '11.0.2'
    modules = ['javafx.controls']
}

dependencies {
    implementation 'io.grpc:grpc-netty:1.58.0' // For gRPC transport
    implementation 'io.grpc:grpc-protobuf:1.58.0' // For Protobuf support
    implementation 'io.grpc:grpc-stub:1.58.0' // For Stub classes
    implementation 'javax.annotation:javax.annotation-api:1.3.2' // Required for annotations

    testImplementation 'junit:junit:4.13.2'
}

// Protobuf plugin configuration
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.23.4'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.58.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11)) // Adjust as needed
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'sa4e.firefly.grpc.Firefly'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.WARN
 }

application {
    mainClass = 'sa4e.firefly.grpc.FireflyObserver'
}

tasks.register('runFirefly', JavaExec) {
    group = 'application'
    description = 'Run the client application'
    mainClass = 'sa4e.firefly.grpc.Firefly'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register("runObserver") {
    group = "application"
    description = "Builds the jar and runs the application."

    dependsOn("jar") // Ensure the jar task runs first
    finalizedBy("run") // Ensure the run task executes after jar

    doLast {
        println("The application has been built and is running.")
    }
}
